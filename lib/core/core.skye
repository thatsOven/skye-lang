import <<"core/internals.h">>;

// compiler built-ins
macro typeOf(item) void; 
macro cast(type, item) void;
macro constCast(ptr) void;
macro concat(strings*) void;

use @typeOf(0u8)  as u8;
use @typeOf(0i8)  as i8;
use @typeOf(0u16) as u16;
use @typeOf(0i16) as i16;
use @typeOf(0u32) as u32;
use @typeOf(0i32) as i32;
use @typeOf(0f32) as f32;
use @typeOf(0u64) as u64;
use @typeOf(0i64) as i64;
use @typeOf(0f64) as f64;
use @typeOf(0usz) as usz;
use @typeOf('\0') as char;

use 0u8 as false;
use 1u8 as true;

macro WINDOWS -> u8;
macro LINUX -> u8;
macro MAC_OS -> u8;
macro UNIX_LIKE -> u8;

macro empty(T) -> T;
macro sizeOf(E) -> usz;

macro nullptr 0;

macro panic(msg) __panicHandler(PANIC_POS, msg);
macro unreachable @panic("Entered unreachable code");

macro returnNone {
    try (?void)::None // trick that uses the way the try operator works to let the compiler infer the return type
}

use (
    u8  | i8  | u16 | i16 | 
    u32 | i32 | u64 | i64 | 
    usz
) as AnyInt;

use f32 | f64 as AnyFloat;

namespace core {
    enum Option[T] {
        Some(T),
        None
    }

    impl[T] core::Option[T] {
        fn or(const self, const value: T) T {
            if self.kind == core::Option::Kind::Some {
                return self.some;
            } else {
                return value;
            }
        }

        fn isSome(const self) u8 {
            return self.kind == core::Option::Kind::Some;
        }

        fn isNone(const self) u8 {
            return self.kind == core::Option::Kind::None;
        }
    }

    enum Result[T, U] {
        Ok(T),
        Error(U)
    }

    impl[T, U] core::Result[T, U] {
        fn or(const self, const value: T) T {
            if self.kind == core::Result::Kind::Ok {
                return self.ok;
            } else {
                return value;
            }
        }

        fn isOk(const self) u8 {
            return self.kind == core::Result::Kind::Ok;
        }

        fn isError(const self) u8 {
            return self.kind == core::Result::Kind::Error;
        }
    }

    struct SliceIterator[T] {
        const slice: *const core::Slice[T],
        idx: usz
    }

    impl[T] core::SliceIterator[T] {
        fn new(const slice: *const core::Slice[T]) Self[T] {
            return core::SliceIterator.{ slice, idx: 0 };
        } 

        fn next(self) ?T {
            return self.slice.get(self.idx++);
        }
    }

    struct Slice[T] {
        const ptr: *const T,
        const length: usz
    }

    impl[T] core::Slice[T] {
        fn new(const ptr: *const T, const length: usz) Self[T] {
            return Self.{ ptr, length };
        }

        fn get(const self, const idx: usz) ?T {
            if idx < self.length {
                return (?T)::Some(self.ptr[idx]);
            } else {
                return (?T)::None;
            }
        }

        fn iter(const self) core::SliceIterator[T] {
            return core::SliceIterator::new(self);
        }
    }

    namespace mem {
        fn blockCopy(from: voidptr, to: voidptr, amt: usz) {
            while amt-- {
                *@cast(*u8, to++) = *@cast(*u8, from++); 
            }
        }

        fn typedBlockCopy[T](from: T, to: T, amt: usz) {
            while amt-- {
                *(to++) = *(from++); 
            } 
        }
    }
}

use core::Slice[char] as String;
use core::Slice as Slice;